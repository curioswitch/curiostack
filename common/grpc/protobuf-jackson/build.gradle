/*
 * MIT License
 *
 * Copyright (c) 2017 Choko (choko@curioswitch.org)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

apply plugin: 'java-library'
apply plugin: 'maven-publish'

apply plugin: 'me.champeau.gradle.jmh'
apply plugin: 'org.curioswitch.gradle-grpc-api-plugin'

archivesBaseName = 'protobuf-jackson'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

[tasks.compileTestJava, tasks.compileJmhJava].each {
    it.sourceCompatibility = '1.8'
    it.targetCompatibility = '1.8'
}

configurations.jmh.extendsFrom configurations.testRuntimeClasspath
jmh {
    exclude = '.*FieldDispatchBenchmark.*'
    warmupIterations = 30
    iterations = 10
    fork = 5
    profilers = []
}

dependencies {
    api 'com.fasterxml.jackson.core:jackson-core'
    api 'com.google.protobuf:protobuf-java'

    implementation 'com.google.protobuf:protobuf-java-util'
    implementation 'net.bytebuddy:byte-buddy'
}

publishing {
    publications {
        maven(MavenPublication) {
            pom.withXml {
                // TODO(choko): Make it simpler to define pom attributes after the artifact, as is
                // normal, by defining a DSL or something.
                asNode().children()[3] + {
                    resolveStrategy = Closure.DELEGATE_FIRST

                    name 'protobuf-jackson'
                    description 'A library for efficient marshalling of Protocol Buffer messages ' +
                            'to and from JSON.'
                    url 'https://github.com/curioswitch/curiostack/tree/master/' +
                            'common/grpc/protobuf-jackson'
                }
            }
        }
    }
}

afterEvaluate {
    tasks.compileTestJava.enabled = true
}
