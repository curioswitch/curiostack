/*
 * MIT License
 *
 * Copyright (c) 2017 Choko (choko@curioswitch.org)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

pluginManagement {
    resolutionStrategy {
        eachPlugin {
            if (requested.id.id == 'org.curioswitch.gradle-curiostack-plugin') {
                useModule "org.curioswitch.curiostack:gradle-curiostack-plugin:${getProperty('org.curioswitch.curiostack.version')}"
            }
            if (requested.id.id == 'org.curioswitch.gradle-grpc-api-plugin') {
                useModule "org.curioswitch.curiostack:gradle-curiostack-plugin:${getProperty('org.curioswitch.curiostack.version')}"
            }
        }
    }
    repositories {
        jcenter()
        gradlePluginPortal()
        maven {
            url 'http://dl.bintray.com/curioswitch/curiostack'
        }
        maven {
            url 'http://palantir.bintray.com/releases'
        }
        mavenLocal()
    }
}

buildscript {
    repositories {
        jcenter()
        gradlePluginPortal()
        maven {
            url 'http://dl.bintray.com/curioswitch/curiostack'
        }
        maven {
            url 'http://palantir.bintray.com/releases'
        }
        mavenLocal()
        // TODO(choko): Remove after https://github.com/etiennestuder/gradle-jooq-plugin/issues/71
        maven {
            url 'https://jitpack.io'
        }
    }
    dependencies {
        classpath "org.curioswitch.curiostack:gradle-curiostack-plugin:${getProperty('org.curioswitch.curiostack.version')}"
    }
}

apply plugin: 'org.curioswitch.gradle-gcloud-build-cache-plugin'

def isCiServer = System.getenv().containsKey('CI')
def isMasterBuild = System.getenv().containsKey('CI_MASTER')

buildCache {
    local {
        enabled = !isCiServer
    }
    remote(org.curioswitch.gradle.plugins.gcloud.buildcache.CloudStorageBuildCache) {
        project = 'curioswitch-cluster'
        bucket = 'curioswitch-gradle-build-cache'
        push = isMasterBuild
    }
}

include ':auth:api', ':auth:server'
include ':common:curio-helpers'
include ':common:google-cloud:cloud-storage'
include ':common:google-cloud:core'
include ':common:google-cloud:iam'
include ':common:google-cloud:pubsub'
include ':common:google-cloud:trace'
include ':common:grpc:protobuf-jackson'
include ':common:server:framework'
include ':common:testing:assertj-protobuf'
include ':common:testing:framework'
include ':cluster:terraform:kube-deployers'
include ':cluster:terraform:sysadmin'
include ':database:yourmakedb:dev'
include ':eggworld:api', 'eggworld:client:web', ':eggworld:server'
include ':gateway:server'
include ':tools:cloudbuild-github-functions'
include ':tools:curiostack-bom'
include ':tools:golang-test'
include ':tools:gradle-plugins:gradle-conda-plugin'
include ':tools:gradle-plugins:gradle-curiostack-plugin'
include ':tools:gradle-plugins:gradle-golang-plugin'
include ':tools:gradle-plugins:gradle-helpers'
include ':tools:gradle-plugins:gradle-protobuf-plugin'
include ':tools:gradle-plugins:gradle-tool-downloader-plugin'
include ':tools:gradle-plugins:java-groovy-compat'
